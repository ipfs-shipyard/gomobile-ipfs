import javax.xml.transform.Source

apply plugin: 'maven-publish'
// apply plugin: 'signing'

task javadoc(type: Javadoc) {
    title = manifest.android_bridge.name
    source = "${buildDir}/source"
    destinationDir = new File("$projectDir/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
     repositories  {
        // external publishing
        maven {
            name = "github" // @MANIFEST
            url = uri("https://maven.pkg.github.com/ipfs-shipyard/gomobile-ipfs") // @MANIFEST
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }

        // local publishing
        maven {
            name = "local"
            url = "${projectDir}/repo" // @MANIFEST
        }
    }

    publications {
        Production(MavenPublication) {
            artifact(javadocJar)
            artifact(project.ext.coreAar)
            artifact(project.ext.coreJar) {
                classifier "sources"
                extension "jar"
            }

            groupId manifest.global.group_id
            artifactId manifest.go_core.android.artifact_id
            version rootProject.ext.version

            pom {
                name = manifest.android_bridge.name
                url = manifest.global.github.url
                description = manifest.go_core.android.description
                packaging = manifest.global.android.packaging

                scm {
                    connection = manifest.global.android.scm.connection
                    developerConnection = manifest.global.android.scm.developer_connection
                    url = manifest.global.android.scm.url
                    }
                }

            pom.withXml {
                def licensesNode = asNode().appendNode('licenses')
                manifest.global.licenses.each {
                    def licenseNode = licensesNode.appendNode('license')
                    licenseNode.appendNode('name', it.name)
                    licenseNode.appendNode('url', it.url)
                    licenseNode.appendNode('distribution', it.distribution)
                }

                def developersNode = asNode().appendNode('developers')
                manifest.global.developers.each {
                    def developerNode = developersNode.appendNode('developer')
                    developerNode.appendNode('id', it.id)
                    developerNode.appendNode('name', it.name)
                    developerNode.appendNode('email', it.email)
                    developerNode.appendNode('organization', it.organization)
                    developerNode.appendNode('organizationUrl', it.organization_url)
                }

                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    dependsOn 'assemble'
}
