plugins {
    id 'com.android.library'
}

def manifest = rootProject.ext.manifest

android {
    compileSdkVersion manifest.global.android.compile_sdk_version
    // buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion manifest.global.android.min_sdk_version
        targetSdkVersion manifest.global.android.target_sdk_version
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'GradleDependency'
        htmlReport false
        warningsAsErrors true
        abortOnError true
    }
}

configurations {
    gomobile
}

ext {
    coreAar = new File("${rootDir}/libs/gomobile/core.aar")
    coreJar = new File("${rootDir}/libs/gomobile/core-sources.jar")
}

dependencies {
    gomobile files(coreAar, coreJar)
    implementation 'androidx.appcompat:appcompat:1.3.0'
}

task extractApi(type: Copy) {
    dependsOn configurations.gomobile

    from {
        configurations.gomobile.collect { zipTree(it) }
    }

    include 'go/*'
    include 'core/*'
    into "$buildDir/source"
}

assemble.dependsOn extractApi

apply from: 'publish.gradle'

